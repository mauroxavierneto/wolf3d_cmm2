'-- RENDER OBJECTS --

SUB DRAW_OBJECT(O)

 LOCAL A_ADJY,B_ADJY

 MAPS(X_OBJ(O)-0.5,Y_OBJ(O)-0.5)=0

 X=(Y_OBJ(O)-Y_POS)*SINVIS+(X_OBJ(O)-X_POS)*COSVIS
 Y=(Y_OBJ(O)-Y_POS)*COSVIS-(X_OBJ(O)-X_POS)*SINVIS

 'REMOVE IMAGE PROCESSING IF THE OBJECT IS OUT OF FIELD OF VIEW
 IF Y<-10 OR Y>0 OR X<-7 OR X>7 THEN EXIT SUB

 'DEBUG PIXELS
 'IF DEBUG=1 THEN
 ' PIXEL X+MM.HRES/2,-Y+MM.VRES/2,RGB(0,255,0)
 ' PIXEL MM.HRES/2,MM.VRES/2,RGB(255,255,255)
 'ENDIF

 TYPE=T_OBJ(O)-1
 IF TYPE<0 THEN EXIT SUB

 Y=ABS(Y)/F_DIST

 IF Y=0 THEN EXIT SUB

 SY1 = B_Y_DIM-64/Y
 SY2 = B_Y_DIM+64/Y

 'CHANGE TEXTURE VIDEO PAGE 
 BY=4+(TYPE << 6) \ 1536 
 XL=(TYPE << 6) MOD 1536
 YL=(BY << 6)

 'PERSPECTIVE CORRECTION OUTSIZE Y WINDOW
 IF SY1 < 4 THEN 
  A_ADJY=(ABS(SY1-4)/(SY2-SY1))*64-1: SY1 = 4: IF A_ADJY<0 THEN A_ADJY=0
  B_ADJY=(ABS(SY2-Y_DIM)/(SY2-SY1))*128+1: SY2 = Y_DIM: IF B_ADJY>63 THEN B_ADJY=63
 ENDIF

 'CUT TEXTURE OF OBJECT IF OFFSCREEN
 IF SY1>=0 AND SY1<=B_Y_DIM THEN 

  T_SIZE=((128/Y+8)/64)*SIZE
  IF T_SIZE<1 THEN T_SIZE=1

  XQA=W_DIM+((X-0.5)*64)/(Y/2.2)
  XQB=(((128/Y)/64)*SIZE)

  'RENDER TEXTURED OBJECT WHEN ON SCREEN
  IF XQA+XQB*(0/SIZE)>0 AND XQA+XQB*(63/SIZE)<MM.HRES-T_SIZE THEN
   'OPTIMIZED VERSION
   FOR T=0 TO 64-SIZE STEP SIZE
    XT=XQA+XQB*(T/SIZE)
    IF SY2>Z_OBJ(XT\SIZE) THEN 
     IMAGE RESIZE_FAST XL+T,YL+A_ADJY, SIZE,64-B_ADJY, XT,SY1, T_SIZE,SY2-SY1, FRAMEBUFFER,1
    ENDIF
   NEXT T
  ELSE
   'SLOWER VERSION (SCREEN CROP)
   FOR T=0 TO 64-SIZE STEP SIZE
    XT=XQA+XQB*(T/SIZE)
    IF XT>A_SCAN AND XT<B_SCAN-T_SIZE THEN
     IF SY2>Z_OBJ(XT\SIZE) THEN 
      IMAGE RESIZE_FAST XL+T,YL+A_ADJY, SIZE,64-B_ADJY, XT,SY1, T_SIZE,SY2-SY1, FRAMEBUFFER,1
     ENDIF
    ENDIF
   NEXT T
  ENDIF

 ENDIF

END SUB

