
'-- START THE OPENNING DOOR ANIMATION --

SUB OPEN_DOOR(X,Y)

 LOCAL T

 IF DOOR_OPENING=1 THEN EXIT SUB
 DOOR_OPENING=1

 T=0
 DO
  IF DOOR(T)=0 THEN
   DOOR(T)=-1
   T_DOOR(T)=MAPW(X,Y)
   MAPD(X,Y)=64
   X_DOOR(T)=INT(X): Y_DOOR(T)=INT(Y)
   PLAY MODSAMPLE 24,3,64,22050
   EXIT DO
  ENDIF
  T=T+1
 LOOP

END SUB


'-- START THE PUSHING SECRET WALL ANIMATION --

SUB PUSH_WALL(X,Y)

 LOCAL T

 IF DOOR_OPENING=1 THEN EXIT SUB
 DOOR_OPENING=1
 S_FLOOR=S_FLOOR+1

 T=0
 DO
  IF DOOR(T)=0 THEN
   DOOR(T)=-64
   T_DOOR(T)=MAPW(X,Y)
   MAPD(X,Y)=-64
   MAPS(X,Y)=-1
   X_DOOR(T)=INT(X): Y_DOOR(T)=INT(Y)
   'SET TO WHERE THE SECRET WALL MUST MOVE
   XS_DOOR(T)=X+COSANG*2: YS_DOOR(T)=Y+SINANG*2
   PLAY MODSAMPLE 23,3,64,22050
   EXIT DO
  ENDIF
  T=T+1
 LOOP

END SUB


'-- START THE CLOSING DOOR ANIMATION --

SUB CLOSE_DOOR(N)

 IF DOOR_OPENING=-1 THEN EXIT SUB

 IF DOOR(N)<0 AND (X_POS+2<X_DOOR(N) OR X_POS-2>X_DOOR(N) OR Y_POS+2<Y_DOOR(N) OR Y_POS-2>Y_DOOR(N)) THEN
  DOOR_OPENING=-1
  MAPD(X_DOOR(N),Y_DOOR(N))=0
  DOOR(N)=1  
  PLAY MODSAMPLE 25,4,64,22050
  E_OPENDOOR=0
 ENDIF

END SUB


'-- DOOR MANAGEMENT --

SUB DOOR_ANIMATION(T)

 LOCAL DOOR_AN

  'DOOR OPENNING OR CLOSING?
  IF DOOR(T)<>0 THEN

   'SET TEMPORARY VARIABLE - READ LESS ARRAY (FASTER)
   DOOR_AN = ABS(MAPD(X_DOOR(T),Y_DOOR(T)))

   'OPEN DOOR
   IF DOOR(T)=-1 THEN
    DOOR_AN=DOOR_AN-8
   'FINISH OPENNING DOOR, SET TIME TO CLOSE IT AND CLEAR BLOCK
    IF DOOR_AN<=0 THEN
     DOOR_AN=0: DOOR_TIME(T)=30: DOOR(T)=-2
     MAPW(X_DOOR(T),Y_DOOR(T))=0
     DOOR_OPENING=0
    ENDIF
   ENDIF

   'PUSH SECRET WALL
   IF DOOR(T)=-64 THEN
    DOOR_AN=DOOR_AN-2
    'VERIFY IF NEED TO CLOSE PUSH EVENT
    IF DOOR_AN<=0 THEN
     'CONTINUE IF DOESN'T HAVE ANY BLOCK IN FRONT OF MOVING SECRET WALL
     'IF MAPW(INT(X_DOOR(T)+XS_DOOR(T)),INT(Y_DOOR(T)+YS_DOOR(T)),0)=0 THEN
      'CONTINUE PUSHING ANIMATION CREATING AN NEW EVENT
     ' PUSH_WALL( INT(X_DOOR(T)+XS_DOOR(T)) , INT(Y_DOOR(T)+YS_DOOR(T)) )
     'ENDIF
     'CLOSE PREVIOUS EVENT
     DOOR_AN=0: DOOR_TIME(T)=0: DOOR(T)=0
     MAPW(X_DOOR(T),Y_DOOR(T))=0
     MAPO(X_DOOR(T),Y_DOOR(T))=0
     MAPD(X_DOOR(T),Y_DOOR(T))=0
     MAPS(X_DOOR(T),Y_DOOR(T))=0
     DOOR_OPENING=0: EXIT SUB
    ENDIF
   ENDIF

   'CLOSE DOOR
   IF DOOR(T)>0 THEN
    DOOR_AN=DOOR_AN+8
    IF DOOR_AN<=8 THEN MAPW(X_DOOR(T),Y_DOOR(T))=T_DOOR(T)
    IF DOOR_AN>=64 THEN DOOR_AN=64: DOOR(T)=0: DOOR_OPENING=0
   ENDIF

   'SET CLOSE TIME
   IF DOOR(T)<>-64 THEN DOOR_TIME(T)=DOOR_TIME(T)-1
   IF DOOR_TIME(T)<0 THEN
    DOOR_TIME(T)=0
    IF DOOR(T)=-2 THEN CLOSE_DOOR(T): DOOR_AN=0
   ENDIF

   'SET DOOR WITH OBJECT ATRIBUTE (SIZE OF DOOR)
   IF DOOR(T)=-64 THEN DOOR_AN=-ABS(DOOR_AN)
   MAPD(X_DOOR(T),Y_DOOR(T))=DOOR_AN

  ENDIF

END SUB

